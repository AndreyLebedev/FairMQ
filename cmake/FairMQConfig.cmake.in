################################################################################
#    Copyright (C) 2018 GSI Helmholtzzentrum fuer Schwerionenforschung GmbH    #
#                                                                              #
#              This software is distributed under the terms of the             #
#              GNU Lesser General Public Licence (LGPL) version 3,             #
#                  copied verbatim in the file "LICENSE"                       #
################################################################################

cmake_minimum_required(VERSION 3.9.4 FATAL_ERROR)

@PACKAGE_INIT@

### General variables for project discovery/inspection
set(FAIRMQ_VERSION @PROJECT_VERSION@)
set(FAIRMQ_GIT_VERSION @FAIRMQ_GIT_VERSION@)

set_and_check(FairMQ_ROOT @PACKAGE_CMAKE_INSTALL_PREFIX@)
set_and_check(FairMQ_BINDIR @PACKAGE_CMAKE_INSTALL_PREFIX@/@FairMQ_INSTALL_BINDIR@)
set_and_check(FairMQ_INCDIR @PACKAGE_CMAKE_INSTALL_PREFIX@/@FairMQ_INSTALL_INCDIR@)
set_and_check(FairMQ_LIBDIR @PACKAGE_CMAKE_INSTALL_PREFIX@/@FairMQ_INSTALL_LIBDIR@)
set_and_check(FairMQ_DATADIR @PACKAGE_CMAKE_INSTALL_PREFIX@/@FairMQ_INSTALL_DATADIR@)
set_and_check(FairMQ_CMAKEMODDIR @PACKAGE_CMAKE_INSTALL_PREFIX@/@FairMQ_INSTALL_CMAKEMODDIR@)

### Import cmake modules
set(CMAKE_MODULE_PATH ${FairMQ_CMAKEMODDIR} ${CMAKE_MODULE_PATH})

### Package dependencies
include(CMakeFindDependencyMacro)

if(    NOT (FairMQ_BOOST_DISABLED OR FairMQ_PACKAGE_DEPENDENCIES_DISABLED)
   AND (@Boost_FOUND@ OR FairMQ_Boost_REQUIRED))
  set(FairMQ_Boost_COMPONENTS system)
  if(FairMQ_ADDITIONAL_Boost_COMPONENTS)
    list(APPEND FairMQ_Boost_COMPONENTS ${FairMQ_ADDITIONAL_Boost_COMPONENTS})
    list(REMOVE_DUPLICATES FairMQ_Boost_COMPONENTS)
  endif()
  if(Boost_INCLUDE_DIR) # checks for cached boost variable which indicates if Boost is already found
    set(SILENCE_BOOST QUIET)
  endif()
  find_dependency(Boost 1.64 ${SILENCE_BOOST}
    COMPONENTS ${FairMQ_Boost_COMPONENTS}
  )
endif()

if(    NOT (FairMQ_OFI_DISABLED OR FairMQ_PACKAGE_DEPENDENCIES_DISABLED)
   AND (@OFI_FOUND@ OR FairMQ_OFI_REQUIRED))
  set(FairMQ_OFI_COMPONENTS fi_sockets)
  if(FairMQ_ADDITIONAL_OFI_COMPONENTS)
    list(APPEND FairMQ_OFI_COMPONENTS ${FairMQ_ADDITIONAL_OFI_COMPONENTS})
    list(REMOVE_DUPLICATES FairMQ_OFI_COMPONENTS)
  endif()
  find_dependency(OFI 1.6.0
    COMPONENTS ${FairMQ_OFI_COMPONENTS}
  )
endif()

### Import targets
include(@PACKAGE_CMAKE_INSTALL_PREFIX@/@PACKAGE_INSTALL_DESTINATION@/FairMQTargets.cmake)

### Alias target for backwards compat (DEPRECATED)
if(NOT TARGET FairRoot::FairMQ)
  add_library(FairRoot::FairMQ ALIAS FairMQ::FairMQ)
endif()

check_required_components(FairMQ)
